
[private]
default:
  @just --list

# show the status of the jobrunner service
status:
    systemctl status -n0 jobrunner
    python3 -m jobrunner.cli.flags get paused

# start the jobrunner service
start:
    sudo systemctl start jobrunner

# stop the jobrunner service
stop:
    sudo systemctl stop jobrunner

# restart the jobrunner service
restart:
    sudo systemctl restart jobrunner

# Update jobrunner to the specified ref & restart
deploy ref="origin/main":
    ~/bin/update-jobrunner.sh {{ ref }}

# update jobrunner dependencies
update-lib:
    git -C jobrunner/lib pull

# run jobrunner migrations
migrate:
    python3 -m jobrunner.cli.migrate

# destroy containers & volumes for all running jobs
prepare-for-reboot: stop
    python3 -m jobrunner.cli.prepare_for_reboot

# stop accepting new jobs
pause:
    python3 -m jobrunner.cli.flags set paused=true

# start accepting new jobs
unpause:
    python3 -m jobrunner.cli.flags set paused=

# show jobrunner logs (limited to 1000 lines) 
logs lines="1000":
    journalctl -xe -u jobrunner -n{{ lines }}

# show jobrunner logs for a specific job_id
logs-id job_id:
    journalctl -u jobrunner | grep {{ job_id }}

# list all currently running jobs
jobs-ls:
    lsjobs

# show docker stats for all currently running jobs
jobs-stats:
    docker stats --no-stream

# retry a specific job
job-retry job_id:
    python3 -m jobrunner.cli.retry_job {{ job_id }}

