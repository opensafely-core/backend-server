export SSL_CERT := "certs/release-hatch.crt"
export SSL_KEY := "certs/release-hatch.key"
export SELFSIGNED_CERT := "certs/testing.crt"
export SELFSIGNED_KEY := "certs/testing.key"
export SYSTEM_CERTS := "/usr/local/share/ca-certificates/release-hatch"

[private]
default:
  @just --list

# pull latest version and deploy
deploy: && restart
    echo "Pulling image"
    docker-compose --no-ansi pull --quiet release-hatch

# restart release-hatch
restart *args:
    docker-compose --no-ansi up --no-build {{ args }} --detach release-hatch

# update to the certificate for release-hatch from backends.opensafely.org
update-certs:
    #!/bin/bash
    set -euo pipefail

    echo "Checking for certificate updates"
    openssl s_client -showcerts -connect backends.opensafely.org:443 </dev/null 2>/dev/null| sed -n '/^-----BEGIN CERT/,/^-----END CERT/p' > certs/fullchain.latest.pem
    update=false
    #
    if ! test -f certs/fullchain.pem; then
        update=true
    # if cert has changed
    elif ! diff -q certs/fullchain.pem certs/fullchain.latest.pem; then
        update=true
    fi

    if test "$update" = "true"; then
        echo "New certificates found, updating and restarting"
        cp certs/fullchain.latest.pem certs/fullchain.pem
        ln -sf fullchain.pem $SSL_CERT
        {{ just_executable() }} restart --force-recreate
    fi
    

[private]
create-test-certificates:
    #!/bin/bash 
    set -euo pipefail

    if test -f $SSL_KEY -o -f $SSL_CERT; then
        echo "$SSL_KEY and/or $SSL_CERT already exist"
        exit;
    fi

    . ~opensafely/config/load-env

    HOSTNAME="$(echo "$RELEASE_HOST" | cut -d'/' -f3 | cut -d':' -f1)"
    mkdir -p "$(dirname $SSL_CERT)"

    # generate a self signed certificate
    openssl req -x509 -newkey ed25519 -keyout $SELFSIGNED_KEY -out $SELFSIGNED_CERT \
        -sha256 -days 365 -nodes \
        -subj "/C=GB/O=OpenSAFELY/CN=$HOSTNAME/emailAddress=test@example.com" \
        -addext "subjectAltName = DNS:$HOSTNAME"

    # Relative link to the selfsigned cert
    # Needs to be relative, as the whole certs/ dir is mounted as a volume
    ln -s $(basename $SELFSIGNED_CERT) $SSL_CERT
    ln -s $(basename $SELFSIGNED_KEY) $SSL_KEY

    # ensure self signed is trusted by this machine
    ln -sf $PWD/$SELFSIGNED_CERT /usr/local/share/ca-certificates/opensafely/
    update-ca-certificates


